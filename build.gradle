plugins {
    // Apply the application plugin to add support for building a CLI application in Java.
    id 'application'
    id("org.openjfx.javafxplugin") version "0.0.13"
    id("maven-publish")
    id 'java'
    id 'org.springframework.boot' version '3.1.4'
    id 'io.spring.dependency-management' version '1.1.3'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

def junitVersion = "5.8.2"

javafx {
    version = "19"
    modules = ['javafx.controls', 'javafx.graphics', 'javafx.fxml', 'javafx.base']
}

dependencies {

    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.2.0'

    // Use JUnit test framework.
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.1'

    // This dependency is used by the application.
    implementation 'com.google.guava:guava:31.0.1-jre'

    // Picocli
    implementation 'info.picocli:picocli:4.6.3'

    // Sentry SDK
    implementation 'io.sentry:sentry:6.10.0'

    implementation 'org.controlsfx:controlsfx:11.1.2'

    // OpenAI-GPT-3
    implementation group: 'com.github.plexpt', name: 'chatgpt', version: '4.0.1'
    implementation 'com.theokanning.openai-gpt3-java:service:0.11.1'

    // JSON Parser
    implementation 'org.json:json:20210307'
}

application {
    // Define the main class for the application.
    mainClass = 'DNAnalyzer.Main'
}

publishing {
    repositories {
        maven {
            name = "Github"
            url = uri("https://maven.pkg.github.com/VerisimilitudeX/DNAnalyzer")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }

    publications {
        register("jar", MavenPublication) {
            from(components["java"])
            groupId = 'live.dnanalyzer'
            artifactId = 'dnanalyzer'
            version = '3.0.0-beta.0'
            pom {
                url.set("https://github.com/VerisimilitudeX/DNAnalyzer.git")
            }
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }

    maxHeapSize = "2g"
}


jar {
    manifest {
        attributes 'Main-Class': 'DNAnalyzer.Main'
    }

    duplicatesStrategy = 'exclude'

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
